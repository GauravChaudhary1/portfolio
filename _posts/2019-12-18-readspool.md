---
title: "Read the spool data with exact fieldcatalog."
description: "How to read the spool data from background report? That too with exact fieldcatalog? Here's how! "
categories: [abap]
tags: [spool, alv]
---

<p>
We, ABAPers, often get this requirement that we need to extract the data out of spool and use it for some purpose like table display, send to excel, send as an email content. You get the jist, right? Then what? We go ahead and create a length according the data we see in the spool and pray that data being retieved doesn't exceed the length limit.
<br>
<img src="{{site.url}}{{site.baseurl}}/images/ABAP/20191220/meme2.jpg">
<br>
We can easily read the spool using different Function Modules and Spool Id. But we always get stuck when we need what kind of data is being retrieved? Why? Because field catalog is missing and what is returned is just plain text file.
<br>
<img src="{{site.url}}{{site.baseurl}}/images/ABAP/20191220/meme1.jpg">
<br><br>
What if I told you that this can be knocked over?<br><br>
<h3>&lt;&lt;ENTERS&gt;&gt; &nbsp;&nbsp;&nbsp;&nbsp; <b> ALV Runtime Services </b></h3>
</p>
<br>
Have a look at a below code:

```abap
"Metadata -> To extract the fieldcatalog, layout etc of the ALV.
"Data -> Data published by ALV

cl_salv_bs_runtime_info=>set( EXPORTING display  = abap_false
                                        metadata = abap_true
                                        data     = abap_true ).
```

<p> Now you have set the parameters, its time to run the report in the background</p>

```abap
SUBMIT (p_name) USING SELECTION-SET p_var AND RETURN.
```

<p> This statement will run your report in the background and still will generate a spool but we will not need it to get the things we want!</p>

<p> Now its time to retrieve the results.</p>

```abap
TRY.
      cl_salv_bs_runtime_info=>get_data_ref( IMPORTING r_data = lr_data ).
      ASSIGN lr_data->* TO <fs_data>.
      IF <fs_data> IS ASSIGNED.
        TRY.
            cl_salv_table=>factory( IMPORTING
                                    r_salv_table   = lr_table
                                  CHANGING
                                    t_table        = <fs_data>  ).
            lt_fieldcat = cl_salv_controller_metadata=>get_lvc_fieldcatalog(
                r_columns      = lr_table->get_columns( ) " ALV Filter
                r_aggregations = lr_table->get_aggregations( ) " ALV Aggregations
        ).
          CATCH cx_root.

        ENDTRY.

        ENDIF.
    CATCH cx_salv_bs_sc_runtime_info.
      
  ENDTRY.

  cl_salv_bs_runtime_info=>clear_all( ).
```

This would simply provide you the results returned during spool generation. Great, right?

<h3> But, how does it work?</h3>
<p> SAP uses session memory to store this information (Only if requested). Once this, request comes by setting the parameters, whatever parameters are passed to ALV gets stored in the session and then extracted from the memory using standard methods.</p>

```abap
" This standard part will trigger the request, that runtime info is requested.
export s_runtime_info from ls_runtime_info to memory id cl_salv_bs_runtime_info=>c_memid_info.

" This code will retrieve the results set by ALV.
import t_component to lt_component from memory id cl_salv_bs_runtime_info=>c_memid_data_def.

" Of course these all happens within standard SAP Classes but SAP Provides a wrapper class 'CL_SALV_BS_RUNTIME_INFO' for us to carry out operations for our needs.
```

Now, yeah, this has an additional advantage over the traditional methods to read spool but it comes with limitations.

<img src="{{site.url}}{{site.baseurl}}/images/ABAP/20191220/distractedjpg.jpeg">

<h3> Let's talk about the limitations: </h3>
<ul>
<li> It will only work when the spool is generated by ALV. It will not work if spool is generated using procedural 'Write' statements.</li>
<li> It doesn't work well if there are multiple ALV's outputed by Report.</li>
<li> Data in memory last's as long as session is active. To store such information for long, explicit code needs to be programmed.</li>


<i>Happy Learning!</i>
</ul>
